;+
;*****************************************************************************************
;
;  PROCEDURE:   expand_index_array.pro
;  PURPOSE  :   This routine is meant to expand upon input start/end elements for an
;                 indexing array or expand the elements of an array of indices, depending
;                 on the settings of the input keywords.  Thus, the input array,
;                 (i.e., defined by the ARR_IN keyword) is assumed to be an array of
;                 indices for some other array of data values.  The purpose of this
;                 routine is to generalize, with error handling, the expansion of the
;                 indexing array in the event that the user wants to look at a, say,
;                 "zoomed-out" view of the current subset of data points defined by the
;                 indices in ARR_IN.
;                 ***  Still under construction  ***
;
;  CALLED BY:   
;               NA
;
;  INCLUDES:
;               NA
;
;  CALLS:
;               is_a_number.pro
;               fill_range.pro
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               NA
;
;  EXAMPLES:    
;               [calling sequence]
;               expand_index_array [,SI__IN=si__in] [,EI__IN=ei__in] [,ARR_IN=arr_in] $
;                                  [,D_AR_IN=d_ar_in] [,EX_A_IN=ex_a_in]              $
;                                  [,SI_OUT=si_out] [,EI_OUT=ei_out] [,ARROUT=arrout] $
;                                  [,D_AROUT=d_arout]
;
;
;               ;;  Cleanup to avoid previous definition contamination
;               DELVAR,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               si__in         = 5703L
;               ei__in         = 5703L
;               expand_index_array,SI__IN=si__in,EI__IN=ei__in,ARR_IN=arr_in, $
;                                  D_AR_IN=d_ar_in,EX_A_IN=ex_a_in,           $
;                                  SI_OUT=si_out,EI_OUT=ei_out,ARROUT=arrout, $
;                                  D_AROUT=d_arout
;               HELP,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               SI__IN          LONG      =         5703
;               EI__IN          LONG      =         5703
;               ARR_IN          LONG      = Array[1]
;               D_AR_IN         LONG      =            1
;               EX_A_IN         LONG      =            1
;               SI_OUT          LONG      =         5702
;               EI_OUT          LONG      =         5704
;               ARROUT          LONG      = Array[3]
;               D_AROUT         LONG      =            1
;               PRINT,';;',arr_in & $
;               PRINT,';;',arrout
;               ;;        5703
;               ;;        5702        5703        5704
;               
;               ;;  Cleanup to avoid previous definition contamination
;               DELVAR,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               si__in         = 5703L
;               ei__in         = 5713L
;               d_ar_in        = 2L
;               expand_index_array,SI__IN=si__in,EI__IN=ei__in,ARR_IN=arr_in, $
;                                  D_AR_IN=d_ar_in,EX_A_IN=ex_a_in,           $
;                                  SI_OUT=si_out,EI_OUT=ei_out,ARROUT=arrout, $
;                                  D_AROUT=d_arout
;               HELP,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               SI__IN          LONG      =         5703
;               EI__IN          LONG      =         5713
;               ARR_IN          LONG      = Array[5]
;               D_AR_IN         LONG      =            2
;               EX_A_IN         LONG      =            2
;               SI_OUT          LONG      =         5701
;               EI_OUT          LONG      =         5715
;               ARROUT          LONG      = Array[7]
;               D_AROUT         LONG      =            2
;               PRINT,';;',arr_in & $
;               PRINT,';;',arrout
;               ;;        5703        5705        5707        5709        5711
;               ;;        5701        5703        5705        5707        5709        5711        5713
;               
;               ;;  Cleanup to avoid previous definition contamination
;               DELVAR,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               si__in         = 5703L
;               ei__in         = 5706L
;               ex_a_in        = 2L
;               expand_index_array,SI__IN=si__in,EI__IN=ei__in,ARR_IN=arr_in, $
;                                  D_AR_IN=d_ar_in,EX_A_IN=ex_a_in,           $
;                                  SI_OUT=si_out,EI_OUT=ei_out,ARROUT=arrout, $
;                                  D_AROUT=d_arout
;               HELP,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               SI__IN          LONG      =         5703
;               EI__IN          LONG      =         5706
;               ARR_IN          LONG      = Array[4]
;               D_AR_IN         LONG      =            1
;               EX_A_IN         LONG      =            2
;               SI_OUT          LONG      =         5701
;               EI_OUT          LONG      =         5708
;               ARROUT          LONG      = Array[8]
;               D_AROUT         LONG      =            1
;               PRINT,';;',arr_in & $
;               PRINT,';;',arrout
;               ;;        5703        5704        5705        5706
;               ;;        5701        5702        5703        5704        5705        5706        5707        5708
;               
;               
;               DELVAR,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               arr_in         = LINDGEN(5) + 5703L
;               d_ar_in        = 2L
;               expand_index_array,SI__IN=si__in,EI__IN=ei__in,ARR_IN=arr_in, $
;                                  D_AR_IN=d_ar_in,EX_A_IN=ex_a_in,           $
;                                  SI_OUT=si_out,EI_OUT=ei_out,ARROUT=arrout, $
;                                  D_AROUT=d_arout
;               HELP,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               SI__IN          LONG      =         5703
;               EI__IN          LONG      =         5707
;               ARR_IN          LONG      = Array[5]
;               D_AR_IN         LONG      =            1
;               EX_A_IN         LONG      =            1
;               SI_OUT          LONG      =         5702
;               EI_OUT          LONG      =         5708
;               ARROUT          LONG      = Array[7]
;               D_AROUT         LONG      =            1
;               PRINT,';;',arr_in & $
;               PRINT,';;',arrout
;               ;;        5703        5704        5705        5706        5707
;               ;;        5702        5703        5704        5705        5706        5707        5708
;               
;               DELVAR,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               arr_in         = LINDGEN(5) + 5703L
;               ex_a_in        = 2L
;               expand_index_array,SI__IN=si__in,EI__IN=ei__in,ARR_IN=arr_in, $
;                                  D_AR_IN=d_ar_in,EX_A_IN=ex_a_in,           $
;                                  SI_OUT=si_out,EI_OUT=ei_out,ARROUT=arrout, $
;                                  D_AROUT=d_arout
;               HELP,si__in,ei__in,arr_in,d_ar_in,ex_a_in,si_out,ei_out,arrout,d_arout
;               SI__IN          LONG      =         5703
;               EI__IN          LONG      =         5707
;               ARR_IN          LONG      = Array[5]
;               D_AR_IN         LONG      =            1
;               EX_A_IN         LONG      =            2
;               SI_OUT          LONG      =         5701
;               EI_OUT          LONG      =         5709
;               ARROUT          LONG      = Array[9]
;               D_AROUT         LONG      =            1
;               PRINT,';;',arr_in & $
;               PRINT,';;',arrout
;               ;;        5703        5704        5705        5706        5707
;               ;;        5701        5702        5703        5704        5705        5706        5707        5708        5709
;
;  KEYWORDS:    
;               ****************
;               ***  INPUTS  ***
;               ****************
;               SI__IN   :  Scalar [integer/long] defining the start index of the input
;                             index array, ARR_IN
;                             [Default = ARR_IN[0] if defined, else 0]
;               EI__IN   :  Scalar [integer/long] defining the end index of the input
;                             index array, ARR_IN
;                             [Default = ARR_IN[N - 1] if defined, else (N  - 1)]
;               ARR_IN   :  [N]-Element [integer/long] array of input indices the user
;                             wishes to expand/contract by EX_A_IN
;                             [Default = LINDGEN(EI__IN - SI__IN + 1)*D_AR_IN + SI__IN]
;               D_AR_IN  :  Scalar [integer/long] defining the difference in value
;                             between adjacent indices in ARR_IN
;                             [Default = 1]
;               EX_A_IN  :  Scalar [integer/long] defining the value by which to expand
;                             ARR_IN (can be negative to contract)
;                             [Default = 1*D_AR_IN]
;               SI_OUT   :  Scalar [integer/long] defining the start element of the output
;                             index array, ARROUT.  Only set if the user wants a subset
;                             of ARR_IN.
;                             [Default = (defined on output)]
;               EI_OUT   :  Scalar [integer/long] defining the end element of the output
;                             index array, ARROUT.  Only set if the user wants a subset
;                             of ARR_IN.
;                             [Default = (defined on output)]
;               ARROUT   :  [M]-Element [integer/long] array of output values for ARROUT.
;                             Only set if user knows the result but not the other inputs
;                             (e.g., D_I__IN)
;                             [Default = (defined on output)]
;               D_AROUT  :  Scalar [integer/long] defining the difference in value
;                             between adjacent indices in ARROUT
;                             [Default = (defined on output)]
;               *****************
;               ***  OUTPUTS  ***
;               *****************
;               SI__IN   :  Returns the scalar start element of the input array, ARR_IN
;               EI__IN   :  Returns the scalar end element of the input array, ARR_IN
;               ARR_IN   :  Returns the [N]-element input array
;                             (Note:  will not change if defined on input)
;               D_AR_IN  :  Returns the scalar inter-value spacing of ARR_IN
;               SI_OUT   :  Returns the scalar start element of the output array, ARROUT,
;                             relative to the range of values for ARR_IN
;                             (i.e., value can be negative)
;               EI_OUT   :  Returns the scalar end element of ARROUT relative to the
;                             range of values for ARR_IN (i.e., value can exceed the
;                             number of elements in ARR_IN)
;               ARROUT   :  Returns the [M]-element output array (i.e., M may not equal N)
;               D_AROUT  :  Returns the scalar inter-value spacing of ARROUT
;                             (Note:  this will should equal D_AR_IN)
;
;   CHANGED:  1)  Continued to write routine
;                                                                   [07/21/2016   v1.0.0]
;
;   NOTES:      
;               1)  *** Minimum Required Input ***
;                   a)  (SI__IN AND EI__IN) OR ARR_IN
;                   b)  (SI__IN AND EI__IN) AND D_AR_IN
;               2)  .compile /Users/lbwilson/Desktop/temp_idl/expand_index_array.pro
;                   expand_index_array,SI__IN=si__in,EI__IN=ei__in,ARR_IN=arr_in, $
;                                      D_AR_IN=d_ar_in,EX_A_IN=ex_a_in,           $
;                                      SI_OUT=si_out,EI_OUT=ei_out,ARROUT=arrout, $
;                                      D_AROUT=d_arout
;
;  REFERENCES:  
;               
;
;   CREATED:  07/20/2016
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  07/21/2016   v1.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

PRO expand_index_array,SI__IN=si__in,EI__IN=ei__in,ARR_IN=arr_in, $  ;;  Inputs
                       D_AR_IN=d_ar_in,EX_A_IN=ex_a_in,           $
                       SI_OUT=si_out,EI_OUT=ei_out,ARROUT=arrout, $  ;;  Outputs
                       D_AROUT=d_arout

;;----------------------------------------------------------------------------------------
;;  Constants
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
;;  Define allowed number types
all_num_type   = [1,2,3,4,5,12,13,14,15]   ;;  All numeric types sorted by type number
nt_by_size     = [1,2,12,3,13,14,15,4,5]   ;;  All numeric types sorted by bit size
all_int_type   = [1,2,12,3,13,14,15]       ;;  Only interger-types sorted by bit size
;;  Dummy error messages
noinpt_msg     = 'User must define at least (SI__IN and EI__IN) or ARR_IN keyword on input...'
badsei_msg     = 'Bad input:  Inputs must satisfy (EI__IN GE SI__IN) and both must be positive-definite integers'
badari_msg     = 'Bad input:  Input array ARR_IN must be a positive-definite array of indices for another array'
;;----------------------------------------------------------------------------------------
;;  Check for input keywords
;;----------------------------------------------------------------------------------------
;;  Check for SI__IN and EI__IN
test_s_in      = is_a_number(si__in,/NOMSSG)
test_e_in      = is_a_number(ei__in,/NOMSSG)
test_se        = (test_s_in[0] AND test_e_in[0])
;;  Check for ARR_IN
test_ar_in     = ((N_ELEMENTS(arr_in) GT 2) AND is_a_number(arr_in,/NOMSSG))
;;  Check for D_AR_IN
test_da_in     = is_a_number(d_ar_in,/NOMSSG)
;;  Check for EX_A_IN
test_exain     = is_a_number(ex_a_in,/NOMSSG)
;;  Make sure user has provided enough input information
test           = (~test_se[0] AND ~test_ar_in[0])
IF (test[0]) THEN BEGIN
  ;;  Not enough input
  MESSAGE,noinpt_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN
ENDIF
;;----------------------------------------------------------------------------------------
;;  Check keyword settings
;;----------------------------------------------------------------------------------------
;;  Check SI__IN and EI__IN
IF ( test_s_in[0]) THEN test_s_in  = (si__in[0] GE 0)          ;;  Make sure value is > 0
IF ( test_e_in[0]) THEN test_e_in  = (ei__in[0] GE 0)          ;;  Make sure value is > 0
IF (   test_se[0]) THEN test_se    = (ei__in[0] GE si__in[0])  ;;  Make sure (end â‰¥ start) element
;;  Make sure input formats are correct
test           = (~test_se[0] AND ~test_ar_in[0])
IF (test[0]) THEN BEGIN
  ;;  Bad input
  MESSAGE,badsei_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN
ENDIF
;;  Check ARR_IN
IF (test_ar_in[0]) THEN test_ar_in = (TOTAL(SIZE(arr_in,/TYPE) EQ all_int_type) GT 0)
;;  Make sure input format is correct
test           = (~test_se[0] AND ~test_ar_in[0])
IF (test[0]) THEN BEGIN
  ;;  Bad input
  MESSAGE,badari_msg[0],/INFORMATIONAL,/CONTINUE
  RETURN
ENDIF
;;  Check D_AR_IN
IF (test_da_in[0]) THEN test_da_in = (d_ar_in[0] GE 1L)  ;;  Define spacing between adjacent values
;;  Check EX_A_IN
IF (test_exain[0]) THEN test_exain = (ABS(ex_a_in[0]) GE 1L)
;;----------------------------------------------------------------------------------------
;;  Define input keywords (if not set by user)
;;----------------------------------------------------------------------------------------
;;  Define spacing between adjacent values (i.e., D_AR_IN)
IF (~test_da_in[0]) THEN d_ar_in = 1L ELSE d_ar_in = LONG(d_ar_in[0])
;;  Define value by which to expand array (i.e., EX_A_IN)
IF (~test_exain[0]) THEN ex_a_in = d_ar_in[0] ELSE ex_a_in = LONG(ex_a_in[0])
;;  First check if both (SI__IN and EI__IN) AND ARR_IN are defined
;;    --> If so, then make sure they are consistent
test           = (test_se[0] AND test_ar_in[0])
IF (test[0]) THEN BEGIN
  n__in          = N_ELEMENTS(arr_in)
  one            = n__in[0]/n__in[0]
ENDIF
;;  Define start/end indices of input array (i.e., SI__IN and EI__IN)
test           = (~test_se[0] AND test_ar_in[0])
IF (test[0]) THEN BEGIN
  n__in          = N_ELEMENTS(arr_in)
  one            = n__in[0]/n__in[0]
  si__in         = arr_in[0L]
  ei__in         = arr_in[n__in[0] - 1L]
  ;;  Redefine spacing and expansion, if necessary
  diff           = arr_in - SHIFT(arr_in,1)
  delt           = LONG(MEDIAN(diff[1:*]))*one[0]
  test_da_in     = (delt[0] EQ d_ar_in[0])
  IF (~test_da_in[0]) THEN d_ar_in = delt[0] > 1
  IF (~test_exain[0]) THEN ex_a_in = d_ar_in[0]
ENDIF
;;  Define input array of indices (i.e., ARR_IN)
test           = (test_se[0] AND ~test_ar_in[0])
IF (test[0]) THEN BEGIN
  n__in  = (ei__in[0] - si__in[0] + 1L)/d_ar_in[0]
  IF (n__in[0] LE 0) THEN BEGIN
    n__in   = LONG64(ei__in[0] - si__in[0] + 1LL)
    d_ar_in = LONG64(d_ar_in[0])
  ENDIF
  arr_in = fill_range(si__in[0],ei__in[0],DIND=d_ar_in[0])
;  IF (n__in[0] LE 0) THEN BEGIN
;    n__in = LONG64(ei__in[0] - si__in[0] + 1LL)
;    arr_in = L64INDGEN(n__in[0])*LONG64(d_ar_in[0]) + LONG64(si__in[0])
;  ENDIF ELSE BEGIN
;    arr_in = LINDGEN(n__in[0])*d_ar_in[0] + si__in[0]
;  ENDELSE
ENDIF
one            = n__in[0]/n__in[0]
;;----------------------------------------------------------------------------------------
;;  Define output parameters
;;----------------------------------------------------------------------------------------
;;  ***********************************
;;  ***  NEEDS a lot of work still  ***
;;  ***********************************
si_out         = si__in[0] - ex_a_in[0]
ei_out         = ei__in[0] + ex_a_in[0]
first          = arr_in[0L]
last           = arr_in[n__in[0] - 1L]
exp_arr        = LINDGEN(ex_a_in[0])*d_ar_in[0] + d_ar_in[0]
before         = first[0] - REVERSE(exp_arr)
after          = last[0] + exp_arr
arrout         = [before,arr_in,after]
;arrout         = [(first[0] - ex_a_in[0]),arr_in,(last[0] + ex_a_in[0])]
diff           = arrout - SHIFT(arrout,1)
d_arout        = LONG(MEDIAN(diff[1:*]))*one[0]
;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------

RETURN
END








