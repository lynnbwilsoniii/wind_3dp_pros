;+
;*****************************************************************************************
;
;  FUNCTION :   lbw_fast_short_ninteg_3d_vdf.pro
;  PURPOSE  :   Performs a numerical integration of a 3D velocity distribution function
;                 (VDF), using Simpson's 1/3 Rule, to find the velocity moments up to
;                 the heat flux vector.  The results are assumed to be in a cartesian
;                 coordinate basis.
;
;  CALLED BY:   
;               NA
;
;  INCLUDES:
;               NA
;
;  CALLS:
;               load_constants_fund_em_atomic_c2014_batch.pro
;               load_constants_extra_part_co2014_ci2015_batch.pro
;               is_a_3_vector.pro
;               mag__vec.pro
;               unit_vec.pro
;               get_rotated_and_triangulated_vdf.pro
;               delete_variable.pro
;               rot_mat.pro
;               rotate_tensor.pro
;
;  REQUIRES:    
;               1)  UMN Modified Wind/3DP IDL Libraries
;
;  INPUT:
;               VELS       :  [K,3]-Element [numeric] array of particle velocities
;                               [i.e., km/s]
;               DATA       :  [K]-Element [numeric] array of phase (velocity) space
;                               densities [e.g., s^(3) cm^(-3) km^(-3)]
;
;  EXAMPLES:    
;               [calling sequence]
;               vmom_str = lbw_fast_short_ninteg_3d_vdf(vels,data [,VLIM=vlim] [,NGRID=ngrid] $
;                                 [,/SLICE2D] [,VFRAME=vframe] [,/ELECTRON] [,/PROTON] $
;                                 [,/ALPHA_P] [,MAG_DIR=mag_dir] [,VEC1=vec1]          $
;                                 [,VEC2=vec2] [,/N_S_ONLY] [,F3D_QH=fxyz_3d]          $
;                                 [,V1DGRID=v1dgrid])
;
;  KEYWORDS:    
;               ***  INPUTS  ***
;               VLIM       :  Scalar [numeric] defining the speed limit for the velocity
;                               grids over which to triangulate the data [km/s]
;                               [Default = max vel. magnitude]
;               NGRID      :  Scalar [numeric] defining the number of grid points in each
;                               direction to use when triangulating the data.  The input
;                               will be limited to values between 30 and 300.
;                               [Default = 101]
;               SLICE2D    :  If set, routine will return a 2D slice instead of a 3D
;                               projection
;                               [Default = TRUE]
;               VFRAME     :  [3]-Element [float/double] array defining the 3-vector
;                               velocity of the K'-frame relative to the K-frame [km/s]
;                               to use to transform the velocity distribution into the
;                               bulk flow reference frame
;                               [ Default = [0,0,0] ]
;               ELECTRON   :  If set, use electron mass [eV/(km/sec)^2]
;                                [Default = TRUE if MASS = FALSE]
;               PROTON     :  If set, use proton mass [eV/(km/sec)^2]
;                                [Default = FALSE]
;               ALPHA_P    :  If set, use alpha-particle mass [eV/(km/sec)^2]
;                               [Default = FALSE]
;               MAG_DIR    :  [3]-Element vector to be used to define the magnetic field
;                               direction to which the cartesian data will be rotated
;                               [ Default = [1,0,0] ]
;               N_S_ONLY   :  If set, routine will return the velocity moment structure
;                               but only with the number density value calculated
;               ***  Pre-Set INPUTS  ***
;               VEC1       :  [3]-Element vector to be used for X-direction in
;                               a 3D rotation of the input data
;                               [e.g., see rotate_3dp_structure.pro]
;                               [ Default = [1.,0.,0.] ]
;               VEC2       :  [3]--Element vector to be used with VEC1 to define a 3D
;                               rotation matrix.  The new basis will have the following:
;                                 X'  :  parallel to VEC1
;                                 Z'  :  parallel to (VEC1 x VEC2)
;                                 Y'  :  completes the right-handed set
;                               [ Default = [0.,1.,0.] ]
;               ***  OUTPUT  ***
;               !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
;               ***  [all the following changed on output]  ***
;               !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
;               F3D_QH     :  Set to a named variable to return the 3D array of phase
;                               space densities triangulated onto a regular grid
;                                [e.g., s^(3) cm^(-3) km^(-3)]
;               V1DGRID    :  Set to a named variable to return the 1D array of regularly
;                               gridded velocities [km/s]
;
;   CHANGED:  1)  NA
;                                                                   [MM/DD/YYYY   v1.0.0]
;
;   NOTES:      0)  Adapted from lbw_nintegrate_3d_vdf.pro and lbw_fast_nintegrate_3d_vdf.pro
;                     to increase computation speed when calling 1000s of times in a row
;
;               Velocity moments:
;                 Note:  d^3v --> v_perp dv_perp dv_para dphi --> π |v_perp| dv_perp dv_para
;                             --> dvx dvy dvz  {for cartesian coordinates}
;
;                 Definitions:
;                   ms        :  particle mass [eV km^(-2) s^(+2)] of species s
;                   h         :  Simpson's 1/3 Rule integration factor [km^(+3) s^(-3)]
;                                = ∆vx*∆vy*∆vz/[3 (N - 1)]^3
;                   S_ijk     :  Simpson's 1/3 Rule coefficients [N/A]
;                   K_ijk     :  common integration factor [km^(+3) s^(-3) sr]
;                                = (h S_ijk)
;                   f_ijk     :  total velocity distribution function (VDF)
;                                [# cm^(-3) km^(-3) s^(+3)]
;                   qmax      :  free-streaming heat flux [eV km s^(-1) cm^(-3)]
;                                = 3/2 me Ne (V_Tec,//)^3
;
;                 Velocity Moments:
;                   Ns        :  Total particle density [cm^(-3)] of species s
;                                = ∑_i ∑_j ∑_k [ K_ijk f_ijk ]
;                   Vos_x     :  X-Component drift velocity [km/s] of species s
;                                = Ns^(-1) ∑_i ∑_j ∑_k [ K_ijk vx_i f_ijk ]
;                   Vos_y     :  Y-Component drift velocity [km/s] of species s
;                                = Ns^(-1) ∑_i ∑_j ∑_k [ K_ijk vy_j f_ijk ]
;                   Vos_z     :  Z-Component drift velocity [km/s] of species s
;                                = Ns^(-1) ∑_i ∑_j ∑_k [ K_ijk vz_k f_ijk ]
;                   Ps_xx     :  XX-Component pressure tensor component [eV cm^(-3)] of
;                                species s
;                                = ms ∑_i ∑_j ∑_k [ K_ijk vx_i vx_i f_ijk ]
;                   Ps_yy     :  YY-Component pressure tensor component [eV cm^(-3)] of
;                                species s
;                                = ms ∑_i ∑_j ∑_k [ K_ijk vy_j vy_j f_ijk ]
;                   Ps_zz     :  ZZ-Component pressure tensor component [eV cm^(-3)] of
;                                species s
;                                = ms ∑_i ∑_j ∑_k [ K_ijk vz_k vz_k f_ijk ]
;                   Ps_xy     :  XY-Component pressure tensor component [eV cm^(-3)] of
;                                species s
;                                = ms ∑_i ∑_j ∑_k [ K_ijk vx_i vy_j f_ijk ]
;                   Ps_xz     :  XZ-Component pressure tensor component [eV cm^(-3)] of
;                                species s
;                                = ms ∑_i ∑_j ∑_k [ K_ijk vx_i vz_k f_ijk ]
;                   Ps_yz     :  YZ-Component pressure tensor component [eV cm^(-3)] of
;                                species s
;                                = ms ∑_i ∑_j ∑_k [ K_ijk vy_j vz_k f_ijk ]
;                   Qs_vec    :  3-Vector version of heat flux tensor [eV km s^(-1) cm^(-3)]
;                                of species s
;                                Qs_x = ms ∑_i ∑_j ∑_k [ K_ijk v^2 vx_i f_ijk ]
;                                Qs_y = ms ∑_i ∑_j ∑_k [ K_ijk v^2 vy_i f_ijk ]
;                                Qs_z = ms ∑_i ∑_j ∑_k [ K_ijk v^2 vz_i f_ijk ]
;
;  REFERENCES:  
;               0)  Simpson's 1/3 Rule References:
;                 http://mathfaculty.fullerton.edu/mathews/n2003/SimpsonsRule2DMod.html
;                 http://www.physics.usyd.edu.au/teach_res/mp/doc/math_integration_2D.pdf
;                 https://en.wikipedia.org/wiki/Simpson%27s_rule
;               1)  See IDL's documentation for:
;                     QHULL.PRO:        https://harrisgeospatial.com/docs/qhull.html
;                     QGRID3.PRO:       https://harrisgeospatial.com/docs/QGRID3.html
;                     TRIANGULATE.PRO:  https://harrisgeospatial.com/docs/TRIANGULATE.html
;                     TRIGRID.PRO:      https://harrisgeospatial.com/docs/TRIGRID.html
;               2)  Carlson et al., "An instrument for rapidly measuring plasma
;                      distribution functions with high resolution,"
;                      Adv. Space Res. 2, pp. 67--70, 1983.
;               3)  Curtis et al., "On-board data analysis techniques for space plasma
;                      particle instruments," Rev. Sci. Inst. 60, pp. 372, 1989.
;               4)  Lin et al., "A Three-dimensional plasma and energetic particle
;                      investigation for the Wind spacecraft," Space Sci. Rev.
;                      Vol. 71, pp. 125, 1995.
;               5)  Paschmann, G. and P.W. Daly "Analysis Methods for Multi-Spacecraft
;                      Data," ISSI Scientific Report, Noordwijk, 
;                      The Netherlands., Int. Space Sci. Inst., 1998.
;               6)  Wilson III, L.B., et al., "The Statistical Properties of Solar Wind
;                      Temperature Parameters Near 1 au," Astrophys. J. Suppl. 236(2),
;                      pp. 41, doi:10.3847/1538-4365/aab71c, 2018.
;               7)  Wilson III, L.B., et al., "Electron energy partition across
;                      interplanetary shocks: I. Methodology and Data Product,"
;                      Astrophys. J. Suppl. 243(8), doi:10.3847/1538-4365/ab22bd, 2019a.
;               8)  Wilson III, L.B., et al., "Electron energy partition across
;                      interplanetary shocks: II. Statistics,"
;                      Astrophys. J. Suppl. 245(24), doi:10.3847/1538-4365/ab5445, 2019b.
;               9)  Wilson III, L.B., et al., "Electron energy partition across
;                      interplanetary shocks: III. Analysis,"
;                      Astrophys. J., Accepted Mar. 4, 2020.
;              10)  Wilson III, L.B., et al., "A Quarter Century of Wind Spacecraft
;                      Discoveries," Rev. Geophys. 59(2), pp. e2020RG000714,
;                      doi:10.1029/2020RG000714, 2021.
;              11)  Wilson III, L.B., et al., "The need for accurate measurements of
;                      thermal velocity distribution functions in the solar wind,"
;                      Front. Astron. Space Sci. 9, pp. 1063841,
;                      doi:10.3389/fspas.2022.1063841, 2022.
;              12)  Wilson III, L.B., et al., "Spacecraft floating potential measurements
;                      for the Wind spacecraft," Astrophys. J. Suppl. 269(52), pp. 10,
;                      doi:10.3847/1538-4365/ad0633, 2023.
;              13)  Gary, S.P., et al., "The whistler heat flux instability: Threshold
;                      conditions in the solar wind," J. Geophys. Res. 99(A12),
;                      pp. 23,391--23,400, doi:10.1029/94JA02067, 1994.
;              14)  Gary, S.P., et al., "Electron heat flux constraints in the solar wind,"
;                      Phys. Plasmas 6(6), pp. 2607--2612, doi:10.1063/1.873532, 1999a.
;              15)  Gary, S.P., et al., "Solar wind electrons: Parametric constraints,"
;                      J. Geophys. Res. 104(A9), pp. 19,843--19,850,
;                      doi:10.1029/1999JA900244, 1999b.
;
;   CREATED:  05/09/2024
;   CREATED BY:  Lynn B. Wilson III
;    LAST MODIFIED:  05/09/2024   v1.0.0
;    MODIFIED BY: Lynn B. Wilson III
;
;*****************************************************************************************
;-

FUNCTION lbw_fast_short_ninteg_3d_vdf,vels,data,VLIM=vlim,NGRID=ngrid,SLICE2D=slice2d,VFRAME=vframe,$
                                                ELECTRON=electron,PROTON=proton,ALPHA_P=alpha_p,    $
                                                MAG_DIR=mag_dir,VEC1=vec1,VEC2=vec2,F3D_QH=fxyz_3d, $
                                                V1DGRID=v1dgrid,N_S_ONLY=n_s_only

;;----------------------------------------------------------------------------------------
;;  Define some constants and dummy variables
;;----------------------------------------------------------------------------------------
f              = !VALUES.F_NAN
d              = !VALUES.D_NAN
;;  Get fundamental and astronomical
@load_constants_fund_em_atomic_c2014_batch.pro
@load_constants_extra_part_co2014_ci2015_batch.pro
c2             = c[0]^2d0                 ;;  (m/s)^2
ckm            = c[0]*1d-3                ;;  m --> km
ckm2           = ckm[0]^2d0               ;;  (km/s)^2
;;  Conversion factors
;;    Energy and Temperature
f_1eV          = qq[0]/hh[0]              ;;  Freq. associated with 1 eV of energy [ Hz --> f_1eV*energy{eV} = freq{Hz} ]
eV2J           = hh[0]*f_1eV[0]           ;;  Energy associated with 1 eV of energy [ J --> J_1eV*energy{eV} = energy{J} ]
eV2K           = qq[0]/kB[0]              ;;  Temp. associated with 1 eV of energy [11,604.5221 K/eV, 2014 CODATA/NIST --> eV2K*energy{eV} = Temp{K}]
K2eV           = kB[0]/qq[0]              ;;  Energy associated with 1 K Temp. [8.6173303 x 10^(-5) eV/K, 2014 CODATA/NIST --> K2eV*Temp{K} = energy{eV}]
;;  Define factor for plasma beta
beta_fac       = 2d0*muo[0]*1d6*eV2J[0]/(1d-9^2)
;;  Define default
def_mass       = me_esa[0]
;;----------------------------------------------------------------------------------------
;;  Define dummy output structure
;;----------------------------------------------------------------------------------------
tags           = ['N_S','VOS_XYZ','PRESS_TENXYZ','PRESS_TENFAC','PRESS_SCALAR',    $
                  'TEMP_3XYZ','TAVG','VTHERMAL','TEMP_3FAC','TANISOTROPY',         $
                  'QFLUX_TENSOR_XZY','QFLUX_FAC_VEC','QFLUX_FAC_NOR','UNITS']
units          = ['cm^(-3)','km/s','eV cm^(-3)','eV cm^(-3)','eV','eV','km/s',     $
                  'eV','N/A','eV km s^(-1) cm^(-3)','eV km s^(-1) cm^(-3)','N/A']
dumb3          = REPLICATE(d,3L)
dumb33         = REPLICATE(d,3L,3L)
dumb333        = REPLICATE(d,3L,3L,3L)
dumb_out       = CREATE_STRUCT(tags,d,dumb3,dumb33,dumb33,d,dumb3,d,d,dumb3,d,dumb333,dumb3,dumb3,units)
;;----------------------------------------------------------------------------------------
;;  Define some tensor maps
;;----------------------------------------------------------------------------------------
;;  Rank-2 tensors
map_r2         = [[0,3,4],[3,1,5],[4,5,2]]       ;;  Rank-2 tensor map
map_v2         = [0,4,8,1,2,5]                   ;;  Symmetry elements of rank-2 tensor
diagr2         = [0,4,8]                         ;;  Diagonal " "
;;  Rank-3 tensors
map_r3         = [[[3,5,2],[5,6,9],[2,9,7]],[[5,6,9],[6,4,1],[9,1,8]],[[2,9,7],[9,1,8],[7,8,0]]]
map_v3         = [26,14,2,0,13,1,4,8,17,5]
ind_r3         = [[0,4,8],[9,13,17],[18,22,26]]
;;----------------------------------------------------------------------------------------
;;  Check input
;;----------------------------------------------------------------------------------------
IF (N_PARAMS() NE 2) THEN RETURN,0b
;;----------------------------------------------------------------------------------------
;;  Check keywords
;;----------------------------------------------------------------------------------------
;;  Check SLICE2D
IF ((N_ELEMENTS(slice2d) EQ 1) AND ~KEYWORD_SET(slice2d)) THEN slice_on = 0b ELSE slice_on = 1b
;;  Check ELECTRON
IF ((N_ELEMENTS(electron) EQ 1) AND KEYWORD_SET(electron)) THEN elec_on = 1b ELSE elec_on = 0b
;;  Check PROTON
IF ((N_ELEMENTS(proton) EQ 1) AND KEYWORD_SET(proton)) THEN prot_on = 1b ELSE prot_on = 0b
;;  Check ALPHA_P
IF ((N_ELEMENTS(alpha_p) EQ 1) AND KEYWORD_SET(alpha_p)) THEN alph_on = 1b ELSE alph_on = 0b
;;  Check mass factors
checks         = [elec_on[0],prot_on[0],alph_on[0]]
IF (TOTAL(checks) GT 1) THEN BEGIN
  ;;  More than one particle type set --> default to electrons
  elec_on        = 1b
  prot_on        = 0b
  alph_on        = 0b
  ;;  Redefine variable
  checks         = [elec_on[0],prot_on[0],alph_on[0]]
ENDIF
good           = WHERE(checks,gd)
CASE good[0] OF
  0L    :  mass = me_esa[0]                           ;;  me [eV km^(-2) s^(+2)]
  1L    :  mass = mp_esa[0]                           ;;  mp [eV km^(-2) s^(+2)]
  2L    :  mass = ma_esa[0]                           ;;  ma [eV km^(-2) s^(+2)]
  ELSE  :  BEGIN
    ;;  Nothing was set --> Default to electrons
    elec_on        = 1b
    prot_on        = 0b
    alph_on        = 0b
    ;;  Redefine variable
    checks         = [elec_on[0],prot_on[0],alph_on[0]]
    ;;  Define mass
    mass           = me_esa[0]                           ;;  me [eV km^(-2) s^(+2)]
  END
ENDCASE
mass_eV        = mass[0]*ckm2[0]                      ;;  [eV km^(-2) s^(+2)] --> [eV]
mass_kg        = mass_eV[0]*qq[0]/c2[0]               ;;  eV --> kg
vtsfac         = SQRT(2d0*eV2J[0]/mass_kg[0])*1d-3    ;;  Conversion factor:  Convert T [eV] to thermal speed [km/s]
;;  Check MAG_DIR
test           = (is_a_3_vector(mag_dir,V_OUT=bdir,/NOMSSG) EQ 0)
IF (test[0]) THEN BEGIN
  brot_on = 0b
  bdir    = [1d0,0d0,0d0]
  bmag    = 1d0
ENDIF ELSE BEGIN
  brot_on = 1b
  bmag    = (mag__vec(bdir,/NAN))[0]
  bdir    = REFORM(unit_vec(bdir,/NAN))
ENDELSE
;;  Check N_S_ONLY
IF KEYWORD_SET(n_s_only) THEN n_s_stop_on = 1b ELSE n_s_stop_on = 0b
;;----------------------------------------------------------------------------------------
;;  Force VEC1 and VEC2
;;----------------------------------------------------------------------------------------
vec1           = [1d0,0d0,0d0]
vec2           = [0d0,1d0,0d0]
;;----------------------------------------------------------------------------------------
;;  Get rotated and triangulated structures
;;----------------------------------------------------------------------------------------
rt_struc       = get_rotated_and_triangulated_vdf(vels,data,VLIM=vlim,NGRID=ngrid,  $
                                                  SLICE2D=slice_on[0],VFRAME=vframe,$
                                                  VEC1=vec1,VEC2=vec2,F3D_QH=fxyz_3d)
;;  Check output
IF (SIZE(rt_struc,/TYPE) NE 8) THEN BEGIN
  MESSAGE,'Failed tetrahedralization: returning without integration...',/INFORMATIONAL,/CONTINUE
  RETURN,dumb_out[0]
ENDIF
;;  Define relevant parameters
v1dgrid        = rt_struc[0].VX2D
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Construct factors for Simpson's 1/3 Rule
;;
;;    Definitions:
;;      h         :  Simpson's 1/3 Rule integration factor [km^(+3) s^(-3)]
;;                   = ∆vx*∆vy*∆vz/[3 (N - 1)]^3
;;      S_ijk     :  Simpson's 1/3 Rule coefficients [N/A]
;;      K_ijk     :  common integration factor [km^(+3) s^(-3) sr]
;;                   = (h S_ijk)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
x              = v1dgrid
y              = v1dgrid
z              = v1dgrid
;;  Define range and increment
nx             = N_ELEMENTS(x)
xran           = [MIN(x,/NAN),MAX(x,/NAN)]
yran           = [MIN(y,/NAN),MAX(y,/NAN)]
zran           = [MIN(z,/NAN),MAX(z,/NAN)]
dx             = (xran[1] - xran[0])/(nx[0] - 1L)
dy             = (yran[1] - yran[0])/(nx[0] - 1L)
dz             = (zran[1] - zran[0])/(nx[0] - 1L)
;;  Construct Simpson's 1/3 Rule coefficients for 1D array
sc             = REPLICATE(1d0,nx[0])
sc[1L:(nx[0] - 2L):2L] *= 4d0              ;;  Start at 2nd element and every other element should be 4
sc[2L:(nx[0] - 3L):2L] *= 2d0              ;;  Start at 3rd element and every other element should be 2
sc[(nx[0] - 1L)]        = 1d0              ;;  Make sure last element is 1
;;  Construct Simpson's 1/3 Rule 2D coefficients
scx            = sc # REPLICATE(1d0,nx[0])
;;  Convert 1D coefficients to 3D
scx3d          = REBIN(scx,nx[0],nx[0],nx[0])
scy3d          = TRANSPOSE(scx3d,[2,0,1])
scz3d          = TRANSPOSE(scy3d,[0,2,1])
scxyz          = scx3d*scy3d*scz3d
;;  Define h-factors for 3D Simpson's 1/3 Rule [assumes regular, uniform grid spacing]
hfac           = dx[0]*dy[0]*dz[0]/(3d0*3d0*3d0)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Integrate to find velocity moments
;;
;;    Definitions:
;;      ms        :  particle mass [eV km^(-2) s^(+2)] of species s
;;      f_ijk     :  total velocity distribution function (VDF)
;;                   [# cm^(-3) km^(-3) s^(+3)]
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Calculate number density [cm^(-3)]
;;      Ns        :  Total particle density [cm^(-3)] of species s
;;                   = ∑_i ∑_j ∑_k [ K_ijk f_ijk ]
;;----------------------------------------------------------------------------------------
ff             = fxyz_3d
;;  Compute 3D integral of input
n_s_tot        = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
IF (FINITE(n_s_tot[0]) EQ 0) THEN STOP
IF (n_s_stop_on[0]) THEN BEGIN
  ;;  User only wants the number density (for testing spacecraft potential values)
  dumb_out[0].N_S = n_s_tot[0]
  RETURN,dumb_out
ENDIF
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Calculate w-component of the drift velocity [km/s] in VFRAME rest frame
;;      Vos_w     :  w-Component drift velocity [km/s] of species s
;;                   = Ns^(-1) ∑_i ∑_j ∑_k [ K_ijk vw_i f_ijk ]
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Convert to 2D
v2dgrid        = v1dgrid # REPLICATE(1d0,nx[0])
;;  Convert to 3D
v3dx           = REBIN(v2dgrid,nx[0],nx[0],nx[0])
v3dy           = TRANSPOSE(v3dx,[2,0,1])
v3dz           = TRANSPOSE(v3dy,[0,2,1])
;;  Calculate X-component of the drift velocity [km/s] in VFRAME rest frame
ff             = v3dx*fxyz_3d/n_s_tot[0]
Vos_x          = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate Y-component of the drift velocity [km/s] in VFRAME rest frame
ff             = v3dy*fxyz_3d/n_s_tot[0]
Vos_y          = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate Z-component of the drift velocity [km/s] in VFRAME rest frame
ff             = v3dz*fxyz_3d/n_s_tot[0]
Vos_z          = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Define output 3-vector bulk velocity [1st moment]
Vos_xyz        = [Vos_x[0],Vos_y[0],Vos_z[0]]
;;  *** Clean up ***
delete_variable,v2dgrid,v3dx,v3dy,v3dz
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Define peculiar velocities (w_i = v - Vos_i)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Refine 1D random velocity variables
x              = v1dgrid - Vos_x[0]
y              = v1dgrid - Vos_y[0]
z              = v1dgrid - Vos_z[0]
;;----------------------------------------------------------------------------------------
;;  Convert 1D random velocity variables to 3D
;;----------------------------------------------------------------------------------------
;;  X
v2dx           = x # REPLICATE(1d0,nx[0])
v3dx           = REBIN(v2dx,nx[0],nx[0],nx[0])          ;;  3rd dimension is just a copy of 2nd
;;  Y
v2dy           = y # REPLICATE(1d0,nx[0])
v3d0           = REBIN(v2dy,nx[0],nx[0],nx[0])
v3dy           = TRANSPOSE(v3d0,[2,0,1])
;;  Z
v2dz           = z # REPLICATE(1d0,nx[0])
v3d0           = REBIN(v2dz,nx[0],nx[0],nx[0])
v3dz           = TRANSPOSE(v3d0,[1,2,0])
;;  *** Clean up ***
delete_variable,v2dgrid,v2dx,v2dy,v2dz,v3d0
;;----------------------------------------------------------------------------------------
;;  Need to update ranges and increments for h factor in Simpson's 1/3 Rule
;;----------------------------------------------------------------------------------------
xran           = [MIN(x,/NAN),MAX(x,/NAN)]
yran           = [MIN(y,/NAN),MAX(y,/NAN)]
zran           = [MIN(z,/NAN),MAX(z,/NAN)]
dx             = (xran[1] - xran[0])/(nx[0] - 1L)
dy             = (yran[1] - yran[0])/(nx[0] - 1L)
dz             = (zran[1] - zran[0])/(nx[0] - 1L)
;;----------------------------------------------------------------------------------------
;;  Update h-factors for 3D Simpson's 1/3 Rule
;;----------------------------------------------------------------------------------------
hfac           = dx[0]*dy[0]*dz[0]/(3d0*3d0*3d0)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Calculate ab-component pressure tensor component [eV cm^(-3)] in species rest frame
;;      Ps_ab     :  ab-Component pressure tensor component [eV cm^(-3)] of
;;                   species s
;;                   = ms ∑_i ∑_j ∑_k [ K_ijk wa_i wb_i f_ijk ]
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Calculate XX-component pressure tensor component [eV cm^(-3)] in species rest frame
ff             = mass[0]*v3dx*v3dx*fxyz_3d
Press_xx       = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate YY-component pressure tensor component [eV cm^(-3)] in species rest frame
ff             = mass[0]*v3dy*v3dy*fxyz_3d
Press_yy       = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate ZZ-component pressure tensor component [eV cm^(-3)] in species rest frame
ff             = mass[0]*v3dz*v3dz*fxyz_3d
Press_zz       = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate XY-component pressure tensor component [eV cm^(-3)] in species rest frame
ff             = mass[0]*v3dx*v3dy*fxyz_3d
Press_xy       = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate XZ-component pressure tensor component [eV cm^(-3)] in species rest frame
ff             = mass[0]*v3dx*v3dz*fxyz_3d
Press_xz       = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate YZ-component pressure tensor component [eV cm^(-3)] in species rest frame
ff             = mass[0]*v3dy*v3dz*fxyz_3d
Press_yz       = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Calculate 3-vector version of heat flux tensor in species rest frame
;;    [eV km s^(-1) cm^(-3)]
;;      Qs_vec    :  abc-Component of heat flux tensor [eV km s^(-1) cm^(-3)]
;;                   of species s
;;                   Qs_x = ms/2 ∑_i ∑_j ∑_k [ K_ijk w^2 wx_i f_ijk ]
;;                   Qs_y = ms/2 ∑_i ∑_j ∑_k [ K_ijk w^2 wy_i f_ijk ]
;;                   Qs_z = ms/2 ∑_i ∑_j ∑_k [ K_ijk w^2 wz_i f_ijk ]
;;    *** Factor of 1/2 in Qflux to account for double-counting or difference in thermal speed definitions???  ***
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Calculate 3D speed
sp3d           = SQRT(v3dx^2d0 + v3dy^2d0 + v3dz^2d0)
;;  Calculate X-component of heat flux vector [eV km s^(-1) cm^(-3)] in species rest frame
ff             = mass[0]*sp3d^2d0*v3dx/2d0*fxyz_3d
Qflux_x        = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate Y-component of heat flux vector [eV km s^(-1) cm^(-3)] in species rest frame
ff             = mass[0]*sp3d^2d0*v3dy/2d0*fxyz_3d
Qflux_y        = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;  Calculate Z-component of heat flux vector [eV km s^(-1) cm^(-3)] in species rest frame
ff             = mass[0]*sp3d^2d0*v3dz/2d0*fxyz_3d
Qflux_z        = TOTAL(hfac[0]*TOTAL(TOTAL(scxyz*ff,3,/NAN),2,/NAN),/NAN)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Define tensors
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Define symmetry components
;;    [xx,yy,zz,xy,xz,yz]
Press_sym_v2   = [Press_xx[0],Press_yy[0],Press_zz[0],Press_xy[0],Press_xz[0],Press_yz[0]]
;;  Define tensors
Press_ten_r2   = Press_sym_v2[map_r2]
Qflux_ten_r1   = REFORM([Qflux_x[0],Qflux_y[0],Qflux_z[0]])
;;  Define temperature pseudotensor [eV]
Temp__ten_r2   = Press_ten_r2/n_s_tot[0]
;;----------------------------------------------------------------------------------------
;;  Define scalar parameters
;;----------------------------------------------------------------------------------------
;;  Scalar/Avg temperature [eV]
Temp__avg_sc   = TOTAL(Temp__ten_r2[diagr2],/NAN)/3d0
;;  Scalar/Avg pressure [eV cm^(-3)]
Press_avg_sc   = TOTAL(Press_ten_r2[diagr2],/NAN)/3d0
;;  Scalar/Avg thermal speed [km/s]
Vthms_avg_sc   = vtsfac[0]*SQRT(Temp__avg_sc[0])
;;----------------------------------------------------------------------------------------
;;  Diagonalize temperature pseudotensor
;;----------------------------------------------------------------------------------------
T3_eigvec      = Temp__ten_r2
LA_TRIRED,T3_eigvec,T3,dumb,/DOUBLE
;;  T3    -->  [3]-element array of diagonal elements of T3_EIGVEC
;;  DUMB  -->  [3]-element array of off-diagonal " "
LA_TRIQL,T3,dumb,T3_eigvec,/DOUBLE,STATUS=status
;;  T3         -->  [3]-element array of eigenvalues
;;  DUMB       -->  destroyed on output
;;  T3_EIGVEC  -->  [3,3]-element array of eigen vectors
IF (status[0] NE 0) THEN T3_eigvec = REPLICATE(d,3L,3L)     ;;  Failed --> fill with NaNs
;;  Sort the output
sp             = SORT(T3)
IF (T3[sp[1]] LT (T3[sp[0]] + T3[sp[2]])/2d0) THEN num = sp[2] ELSE num = sp[0]
;;  Shift the eigenvalues and vectors from smallest to largest
shft           = ([-1,1,0])[num[0]]
T3             = SHIFT(T3,shft)
T3_eigvec      = SHIFT(T3_eigvec,0,shft)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Rotate into field-aligned coordinates (FACs) [if user desires]
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
IF (brot_on[0]) THEN BEGIN
  b_dot_s        = TOTAL((bdir # [1,1,1])*T3_eigvec,1,/NAN)
  dummy          = MAX(ABS(b_dot_s),numx,/NAN)
  mrot           = rot_mat(bdir)
  ;;  Rotate pressure tensor  [xx,yy,zz,xy,xz,yz] --> [perp1,perp2,para,p1p2,p1a,p2a]
  Press_fac_r2   = rotate_tensor(Press_ten_r2,mrot)
  Press_fac_v2   = Press_fac_r2[map_v2]
  ;;  Define temperature [eV] components in FACs  [perp1,perp2,para]
  T3FAC          = Press_fac_v2[0:2]/n_s_tot[0]
  ;;  Define temperature anisotropy Tperp/Tpara
  Tanis          = (T3FAC[0] + T3FAC[1])/T3FAC[2]/2d0
  ;;  Rotate heat flux tensor [eV km s^(-1) cm^(-3)]
  Qflux_fac_r1   = rotate_tensor(Qflux_ten_r1,mrot)
  ;;  Define field-aligned heat flux vector [eV km s^(-1) cm^(-3)]
  Qflux_fac_v3   = Qflux_fac_r1
  ;;  Define free-streaming heat flux [eV km s^(-1) cm^(-3)]
  ;;    q_s,tot = 3/2 ms Ns (V_Ts,//)^3
  ;;        where V_Tj is the 1D rms thermal speed here so it differs by a factor of
  ;;        two from the 1D most probable speed, thus the 2^(-3/2) below
  ;;    [e.g., Gary et al., 1999 Phys. Plasmas Vol. 6(6)]
  vts_para       = vtsfac[0]*SQRT(T3FAC[2])
  qet_norm       = 3d0*mass[0]*n_s_tot[0]*vts_para[0]^3d0/2d0/(2d0^(3d0/2d0))
  ;;  Define normalized heat flux vector [perp1,perp2,para]
  Qflux_fac_n3   = Qflux_fac_v3/qet_norm[0]
ENDIF ELSE BEGIN
  Press_fac_v2   = d*Press_ten_v2
  T3FAC          = d*T3
  Tanis          = d
  Qflux_fac_v3   = REPLICATE(d,3L)
  Qflux_fac_n3   = Qflux_fac_v3
ENDELSE
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Define output structure
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
tags           = ['N_S','VOS_XYZ','PRESS_TENXYZ','PRESS_TENFAC','PRESS_SCALAR',    $
                  'TEMP_3XYZ','TAVG','VTHERMAL','TEMP_3FAC','TANISOTROPY',         $
                  'QFLUX_3VEC_XYZ','QFLUX_3VEC_FAC','QFLUX_3VEC_NOR','UNITS']
units          = ['cm^(-3)','km/s','eV cm^(-3)','eV cm^(-3)','eV','eV','km/s',     $
                  'eV','N/A','eV km s^(-1) cm^(-3)','eV km s^(-1) cm^(-3)','N/A',  $
                  '3VEC_FAC = [perp1,perp2,para]']
struc          = CREATE_STRUCT(tags,n_s_tot[0],Vos_xyz,Press_ten_r2,Press_fac_r2,       $
                                    Press_avg_sc[0],T3,Temp__avg_sc[0],Vthms_avg_sc[0], $
                                    T3FAC,Tanis[0],Qflux_ten_r1,Qflux_fac_v3,           $
                                    Qflux_fac_n3,units)
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;  Return to user
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------
;;----------------------------------------------------------------------------------------

RETURN,struc[0]
END
















































